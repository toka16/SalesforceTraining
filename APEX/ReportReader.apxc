public class ReportReader {

    public static FullReportData getReportData(ID id){
        Reports.ReportResults results = 
            Reports.ReportManager.runReport(id, true);
        
        Reports.ReportMetadata reportMetadata = results.getReportMetadata();
    	List<String> fieldNames = reportMetadata.getDetailColumns();
        
        Reports.ReportExtendedMetadata reportExtendedMetadata = results.getReportExtendedMetadata();
        Map<String, Reports.DetailColumn> detailColumnMap = reportExtendedMetadata.getDetailColumnInfo();
        
        List<FullReportData.ColumnInfo> columnInfo = new List<FullReportData.ColumnInfo>();
        for (String fieldName: fieldNames) {
        	Reports.DetailColumn detailColumn = detailColumnMap.get(fieldName);
            FullReportData.ColumnInfo info = new FullReportData.ColumnInfo(
            	detailColumn.getLabel(), detailColumn.getName(), detailColumn.getDataType().name()
            );
            columnInfo.add(info);
        }
        
        Reports.ReportFactWithDetails factDetails = (Reports.ReportFactWithDetails)results.getFactMap().get('T!T');
        List<Reports.ReportDetailRow> reportDetailRowList = factDetails.getRows();
        
        List<Map<String, FullReportData.CellInfo>> rowsData = new List<Map<String, FullReportData.CellInfo>>();
		for (Reports.ReportDetailRow reportDetailRow: reportDetailRowList) {
        	List<Reports.ReportDataCell> cells = reportDetailRow.getDataCells();
            Map<String, FullReportData.CellInfo> rowData = new Map<String, FullReportData.CellInfo>();
            for (Integer i=0; i<cells.size(); i++){
                Reports.ReportDataCell reportDataCell = cells.get(i);
                FullReportData.CellInfo ci = new FullReportData.CellInfo();
                ci.label = reportDataCell.getLabel();
                rowData.put(fieldNames.get(i), ci);
            }
            rowsData.add(rowData);
        }
        
        FullReportData result = new FullReportData();
        result.columnsData = columnInfo;
        result.rowsData = rowsData;
        
        return result;
    }
}